# Tested Stack

```elixir
Mix.install([
  {:youtube, github: "brooklinjazz/youtube"},
  {:hidden_cell, github: "brooklinjazz/hidden_cell"}
])
```

## Navigation

[Return Home](../start.livemd)<span style="padding: 0 30px"></span>
[Report An Issue](https://github.com/DockYard-Academy/beta_curriculum/issues/new?assignees=&labels=&template=issue.md&title=)

## Tested Stack

Previously you created a [Stack](./stack_server.livemd) [GenServer](https://hexdocs.pm/elixir/GenServer.html). Now you're going to re-create the `Stack` [GenServer](https://hexdocs.pm/elixir/GenServer.html) with a full test suite.

The `Stack` process should store a list as state and `push/2` elements onto the stack, and `pop/2` elements off of the stack like so.

<!-- livebook:{"force_markdown":true} -->

```elixir
{:ok, pid} = Stack.start_link([])
[:one] = Stack.push(pid, :one)
[:two, :one] = Stack.push(pid, :two)
:two = Stack.pop(pid)
:one = Stack.pop(pid)
# return no value when the stack is empty
nil = Stack.pop(pid)
```

<!-- livebook:{"break_markdown":true} -->

### Pair Testing

This will be a **pair testing** exercise. One student will be the **tester** and the other will be the **implementer**. The **tester** will write the test, and the **implementer** will implement the code for the test.

As the **implementer** you may challenge your **tester** to improve their tests by writing only the code necessary to make the test pass (within reason) even if it is a false-positive. This will encourage your **tester** to write more comprehensive tests.

Swap roles for each new test.

<!-- livebook:{"break_markdown":true} -->

### Create A Mix Project

Create a new mix project and use LiveShare to make it simpler to collaborate with your pair partner.

```
mix new stack
```

Copy the code below into your `Stack` module.

```elixir
defmodule Stack do
  use GenServer

  def start_link(initial_state \\ []) do
    GenServer.start_link(__MODULE__, initial_state)
  end

  def push(pid, element) do
    GenServer.call(pid, {:push, element})
  end

  def pop(pid) do
    GenServer.call(pid, :pop)
  end

  @impl true
  def init(initial_state) do
    {:ok, initial_state}
  end

  @impl true
  def handle_call(:pop, _, []) do
    {:reply, nil, []}
  end

  @impl true
  def handle_call(:pop, _, [top | rest]) do
    {:reply, top, rest}
  end

  @impl true
  def handle_call({:push, item}, _, state) do
    new_state = [item | state]
    {:reply, new_state, new_state}
  end
end
```

Then copy the following code into the associated test file for the `Stack` module.

```elixir
ExUnit.start(auto_run: false)

defmodule StackTest do
  use ExUnit.Case

  describe "start_link/1" do
    test "with no configuration default" do
      {:ok, pid} = Stack.start_link()
      assert Stack.pop(pid) == nil
    end

    test "with no configuration" do
      {:ok, pid} = Stack.start_link([])
      assert Stack.pop(pid) == nil
    end

    test "with default state" do
      {:ok, pid} = Stack.start_link([:starter])
      assert Stack.pop(pid) == :starter
    end
  end

  describe "push/2" do
    test "an element onto an empty stack" do
      {:ok, pid} = Stack.start_link()
      assert Stack.pop(pid) == nil
      assert Stack.push(pid, :first) == [:first]
    end

    test "an element onto a stack with one element" do
      {:ok, pid} = Stack.start_link([:first])
      assert Stack.push(pid, :second) == [:second, :first]
    end

    test "an element onto a stack with multiple elements" do
      {:ok, pid} = Stack.start_link([:third, :second, :first])
      assert Stack.push(pid, :fourth) == [:fourth, :third, :second, :first]
      assert Stack.push(pid, :fifth) == [:fifth, :fourth, :third, :second, :first]
    end
  end

  describe "pop/1" do
    test "an empty stack" do
      {:ok, pid} = Stack.start_link()
      assert Stack.pop(pid) == nil
    end

    test "a stack with one element" do
      {:ok, pid} = Stack.start_link([:starter])
      assert Stack.pop(pid) == :starter
      assert Stack.pop(pid) == nil
    end

    test "a stack with multiple elements" do
      {:ok, pid} = Stack.start_link([:third, :second, :first])
      assert Stack.pop(pid) == :third
      assert Stack.pop(pid) == :second
      assert Stack.pop(pid) == :first
      assert Stack.pop(pid) == nil
    end
  end
end

ExUnit.run()
```

## Bonus: GitHub Repository

Upload your stack project to GitHub. You may add your partner as a collaborator, or they can fork the project so that you both own a copy.

Create a README that describes the purpose of the project and what you learned.

## Commit Your Progress

Run the following in your command line from the curriculum folder to track and save your progress in a Git commit.
Ensure that you do not already have undesired or unrelated changes by running `git status` or by checking the source control tab in Visual Studio Code.

```
$ git checkout main
$ git checkout -b exercise-stack
$ git add .
$ git commit -m "finish stack exercise"
$ git push origin exercise-stack
```

**DockYard Academy Students Only:**

Create a pull request to your forked `main` branch and notify your teacher by including `@BrooklinJazz` in your PR description to get feedback.

If you are interested in joining the next academy cohort, [sign up here](https://academy.dockyard.com/) to receive more news when it is available.

## Up Next

| Previous                                                   | Next                                                           |
| ---------------------------------------------------------- | -------------------------------------------------------------: |
| [Testing GenServers](../reading/testing_genservers.livemd) | [Games Score Tracker](../exercises/games_score_tracker.livemd) |
